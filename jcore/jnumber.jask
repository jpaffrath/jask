; Provides standard functions for number processing

function max(n1:n2) if n1 greater n2 return n1 else return n2 endif end
function min(n1:n2) if n1 smaller n2 return n1 else return n2 endif end

function PI() return 3.141592653589793 end
function E() return 2.718281828459045 end

function fibonacci(max)
	store 0 in first
	store 1 in second
	store 0 in next
	store 1 in i
	store list(0) in numbers

	assign max minus 1 to max

	run i to max with i plus 1
		if i smallerequal 1 assign i to c
		else assign first plus second to next assign second to first assign next to second
		endif
		assign listAdd(numbers:next) to numbers
	endrun

	return numbers
end

function pow(a:x)
	if isNumber(a) equals FALSE return NULL else endif
	if isNumber(x) equals FALSE return NULL else endif

	return _pow(a:x)
end

function abs(x)
	store 0 in m
	assign x times -1 to m

	if x smaller 0
		return m
	else
		return x
	endif
end

function root(x:n)
	if x smaller 0 return NULL else endif

	store n in m
	assign m minus 1 to m
	
	store x in xn
	assign xn divide 2 to xn
	store pow(xn:n) in p
	
	store 0 in xnext
	assign m times p to xnext
	assign xnext plus x to xnext

	store 0 in tmp
	assign pow(xn:m) to p
	assign n times p to tmp
	assign xnext divide tmp to xnext
	
	store 0.0000000001 in EPSILON
	store 0 in error
	store 0 in trash
	store 0 in i

	run i to 100 with i plus 1
		assign abs(xnext) to error
		assign abs(xn) to trash
		assign error minus trash to error
		assign abs(error) to error

		if error smaller EPSILON return xnext else endif

		assign xnext to xn
		assign pow(xn:n) to p
		assign m times p to xnext
		assign xnext plus x to xnext
		assign pow(xn:m) to p
		assign n times p to tmp
		assign xnext divide tmp to xnext
	endrun

	return xnext
end

function sqrt(x)
	return root(x:2)
end

function factorial(x)
	if x smaller 0 return NULL else endif

	store 1 in fac
	store 1 in i

	while i smallerequal x
		assign fac times i to fac
		increment i
	endrun

	return fac
end

function hasPlaces(n)
	convert n to string
	assign listFromString(n) to n
	store 1 in i

	while i smaller listSize(n)
		if listGet(n:i) equals "."
			return TRUE
		else endif
		increment i
	endrun
	return FALSE
end